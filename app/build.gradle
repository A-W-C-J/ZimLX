apply plugin: 'com.android.application'
if (enable_plugin_kotlin) {
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'
}

android {
    signingConfigs {
    }
    compileSdkVersion 26
    buildToolsVersion '27.0.3'
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion version_setup_targetSdk

        flavorDimensions "versionCode"

        versionCode 20180519
        versionName '0.2.3'

        applicationId "org.zimmob.zimlx"
        resValue 'string', 'app_name', "ZimLX"
        resValue "string", "manifest_package_id", "org.zimmob.zimlx"
        multiDexEnabled false


        buildConfigField "boolean", "IS_TEST_BUILD", "false"
        buildConfigField "boolean", "IS_GPLAY_BUILD", "false"
        buildConfigField "String[]", "DETECTED_ANDROID_LOCALES", "${findUsedAndroidLocales()}"
        buildConfigField "String", "GITHASH", "\"${getGitHash()}\""

    }
    productFlavors {
        flavorDefault {
            minSdkVersion 25
            targetSdkVersion 26
            applicationId 'org.zimmob.zimlx'
        }
        flavorGplay {
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
            minSdkVersion 25
            targetSdkVersion 26
        }
        flavorTest {
            buildConfigField "boolean", "IS_TEST_BUILD", "true"
            resValue 'string', 'app_name', "OL Nightly"
            versionCode = Integer.parseInt(new Date().format('yyMMdd'))
            versionName = new Date().format('yyMMdd')
            applicationId "io.github.openlauncherteam.openlauncher.test"
            minSdkVersion 25
            targetSdkVersion 26
        }
        ZimLxDebug {
        }
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
        abortOnError false
    }
    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == 'com.android.support') {
                    if (details.requested.name != 'multidex' && details.requested.name != 'multidex-instrumentation') {
                        details.useVersion "${version_library_appcompat}"
                    }
                }
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            pseudoLocalesEnabled true
        }
    }
    sourceSets {
        if (enable_plugin_kotlin) {
            main.java.srcDirs += 'src/main/kotlin'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
    }
}

dependencies {
    // Support libraries
    implementation "com.android.support:recyclerview-v7:${version_library_appcompat}"
    implementation "com.android.support:appcompat-v7:${version_library_appcompat}"
    implementation "com.android.support:cardview-v7:${version_library_appcompat}"
    implementation "com.android.support:design:${version_library_appcompat}"
    implementation "com.android.support:support-v13:${version_library_appcompat}"
    implementation "com.android.support:preference-v7:${version_library_appcompat}"
    implementation "com.android.support:preference-v14:${version_library_appcompat}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    // UI Libs
    implementation 'com.mikepenz:fastadapter:3.2.3@aar'
    implementation 'com.mikepenz:fastadapter-commons:3.0.0@aar'
    implementation 'com.mikepenz:fastadapter-extensions:3.0.0@aar'
    implementation 'jp.wasabeef:recyclerview-animators:2.2.7'
    implementation 'com.eightbitlab:blurview:1.3.3'
    implementation 'com.prolificinteractive:material-calendarview:1.4.3'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.balysv:material-ripple:1.0.2'
    implementation 'agency.tango.android:material-intro-screen:0.0.5'
    implementation 'cat.ereza:customactivityoncrash:2.2.0'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation 'com.jaredrummler:colorpicker:1.0.1'
    implementation 'in.championswimmer:SimpleFingerGestures_Android_Library:1.2'
    implementation 'com.github.daniel-stoneuk:material-about-library:2.3.0'
    implementation('com.github.ozodrukh:CircularReveal:2.0.1@aar') {
        transitive = true
    }
    implementation('com.github.turing-tech:MaterialScrollBar:12.4.0') {
        exclude group: 'com.android.support'
    }
    // Tools
    implementation "com.jakewharton:butterknife:${version_library_butterknife}"
    if (enable_plugin_kotlin) {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$version_plugin_kotlin"
    }
    // Processors
    def anpros = ["com.jakewharton:butterknife-compiler:${version_library_butterknife}"]
    for (anpro in anpros) {
        if (enable_plugin_kotlin) {
            kapt anpro
        } else {
            annotationProcessor anpro
        }
    }
}
repositories {
    mavenCentral()
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'